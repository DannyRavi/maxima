/*************** -*- Mode: MACSYMA; Package: MAXIMA -*-  ******************/

(kill(all), done);
done;

/* SF bug #3208 Can't solve this simple equation */
algsys([x^2+y^2=1,(x-2)^2+(y-3)^2=9],[x,y]);
[[x = -((3^(5/2)-10)/26),y = (2*3^(3/2)+15)/26],
 [x = (3^(5/2)+10)/26,y = -((2*3^(3/2)-15)/26)]];

/* Reduced from SF bug #3208 above */
algsys([676*y^2-20*3^(5/2)-333,676*y^2-4056*y+28*3^(7/2)+2007],[y]);
[[y = (2*3^(3/2)+15)/26]];

/* Reduced from SF bug #3208 above */
algsys([676*y^2+20*3^(5/2)-333,676*y^2-4056*y-28*3^(7/2)+2007],[y]);
[[y = -((2*3^(3/2)-15)/26)]];

/* from example(solve) */
algsys([4*x^2-y^2=12,x*y-x=2],[x,y]);
[[x = 2, y = 2],
 [x = 0.5202594388652008*%i - 0.1331240357358706,
  y = 0.07678378523787788 - 3.608003221870287*%i],
 [x = -0.5202594388652008*%i - 0.1331240357358706,
  y = 3.608003221870287*%i + 0.07678378523787788],
 [x = -1.733751846381093, y = -0.1535675710019696]];

/* failures from example(algsys) during testing */
block( [
     %rnum:0,
     f1:2*x*(1-l1)-2*(x-1)*l2,
     f2:l2-l1,
     f3:l1*(1-x**2-y),
     f4:l2*(y-(x-1)**2)
   ],
   algsys([f1,f2,f3,f4],[x,y,l1,l2]));
[[x = 0, y = %r1, l1 = 0, l2 = 0],
 [x = 1, y = 0,   l1 = 1, l2 = 1]];

block( [
   f1:x**2-y**2,
   f2:x**2-x+2*y**2-y-1
 ],
 algsys([f1,f2],[x,y]));
[[x = -(1/sqrt(3)), y =   1/sqrt(3)],
 [x =   1/sqrt(3),  y = -(1/sqrt(3))],
 [x = -(1/3),y = -(1/3)],
 [x = 1,y = 1]];

/* failure in example(charpoly) during testing */
 algsys([x2-2*x1=0,x2^2+x1^2=1],[x1,x2]);
 [[x1 = -(1/sqrt(5)), x2 = -(2/sqrt(5))],
  [x1 =   1/sqrt(5),  x2 =   2/sqrt(5)]];

block( [%rnum:0],
  algsys([g526+((sqrt(a^2+4)-a)/2+a)*g525,
          ((sqrt(a^2+4)-a)*g526)/2+g525],
	 [g525,g526]));
[[g525 = %r1,g526 = -(%r1*(sqrt(a^2+4)+a))/2]];

block( [%rnum:0],
  algsys([g625+(a-(sqrt(a^2+4)+a)/2)*g624,
           g624-((sqrt(a^2+4)+a)*g625)/2],
	   [g624,g625]));
[[g624 = %r1,g625 = (%r1*(sqrt(a^2+4)-a))/2]];

/* Working example from bug #3150 */
algsys([x^2+y^2=1, x^2+(y-3)^2=9],[x,y]);
[[x = -sqrt(35)/6,y = 1/6],
 [x = sqrt(35)/6,y = 1/6]];

/* Working example from bug #2736 */
algsys([x^2+y^2=1,(x-2)^2+(y-3)^2=10],[x,y]);
[[x = 1,y = 0],[x = -5/13,y = 12/13]];

/* bug #1106 only found 4 roots (of 6)
   Now crash with heap exhausted
 */
/*
block( [
  p1:-x*y^3+y^2+x^4-9*x/8,
  p2:y^4-x^3*y-9*y/8+x^2],
  algsys([p1,p2],[x,y]));
[[x = 1/2,y = 1],
 [x = 9/8,y = 9/8],
 [x = 1,y = 1/2],
 [x = 0,y = 0]]
 */

/* Above can be solved with
   load(grobner)$
   eqs:poly_reduced_grobner([p1,p2],[x,y])$
   algsys(eqs,[x,y]);
 */
algsys([-200704*y^8+437832*y^5-263633*y^2+53010*x,
        4096*y^10-10440*y^7+7073*y^4-729*y],[x,y]);
[[x = 0, y = 0],
 [x = 1, y = 1/2],
 [x = 9/8, y = 9/8],
 [x = 1/2, y = 1],
 [x = (sqrt(3)*%i-1)/4,     y = -((sqrt(3)*%i+1)/2)],
 [x = -((sqrt(3)*%i+1)/4),  y = (sqrt(3)*%i-1)/2],
 [x = (3^(5/2)*%i-9)/16,    y = -((3^(5/2)*%i+9)/16)],
 [x = -((3^(5/2)*%i+9)/16), y = (3^(5/2)*%i-9)/16],
 [x = (sqrt(3)*%i-1)/2,     y = -((sqrt(3)*%i+1)/4)],
 [x = -((sqrt(3)*%i+1)/2),  y = (sqrt(3)*%i-1)/4]];

/* bug #1266 no solution found */
algsys([x^2+x+1=0,x^2*y+1=0],[x,y]);
[[x =   (sqrt(3)*%i-1)/2,  y = -((sqrt(3)*%i-1)/2)],
 [x = -((sqrt(3)*%i+1)/2), y =   (sqrt(3)*%i+1)/2]];

/* bug #1302 no solution found.  Also bug #1469 */
algsys([(x-x0)^2+(y-y0)^2=r^2,(x-x1)^2+(y-y1)^2=r^2],[x,y]);
[]; /* wrong, but at least it returns */

/* bug #1369 algsys hangs */
/*
eq1 : a*x + c*y + d*y^2/2 = 0;
eq2 : b*x + e*x^2 + f*y - g*y^3 = h;
algsys([eq1,eq2],[x,y]);
*/

/* bug #1370 Can solve [F=0,F-G=0] but not [F,G]
   there are some work arounds in the report
 */
F:2*x^3*y^2+2*a*x^2*y^2+3*x^2*y+2*a*x*y+x+1;
2*x^3*y^2+2*a*x^2*y^2+3*x^2*y+2*a*x*y+x+1;
G:x^3*y^2+2*a*x^2*y^2+a^2*x*y^2+x^2*y+2*a*x*y+a^2*y+1;
x^3*y^2+2*a*x^2*y^2+a^2*x*y^2+x^2*y+2*a*x*y+a^2*y+1;
/* FIXME:  Testsuite issue.  Can't match form of solution
algsys([F=0,F-G=0],[x,y]);
[[x = -(((-a^3)+sqrt(a^2-4*a)*(a^2-2*a)+4*a^2)/(2*sqrt(a^2-4*a))),
  y = -(1/(a*sqrt(a^2-4*a)))],
 [x = -((a^3+sqrt(a^2-4*a)*(a^2-2*a)-4*a^2)/(2*sqrt(a^2-4*a))),
  y = 1/(a*sqrt(a^2-4*a))]];
*/

/* hangs
algsys([F=0,G=0],[x,y]);
[];
 */

/* Can solve with 
   eqs:poly_reduced_grobner([F,G],[x,y])
   algsys:(eqs,[x,y]);
 */
/* FIXME:  Testsuite issue.  Can't match form of solution
algsys([(a^4-4*a^3)*y^2-1,(a^4-4*a^3)*y+2*x+a^2-2*a],[x,y]);
[[x = -((a*sqrt(a^2-4*a)+a^2-2*a)/2), y =   sqrt(a^2-4*a)/(a^3-4*a^2)],
 [x =   (a*sqrt(a^2-4*a)-a^2+2*a)/2,  y = -(sqrt(a^2-4*a)/(a^3-4*a^2))]];
*/
 
/* Bug #1647 */
algsys([x^2+y^2-d^2, (x-h)^2+(y-k)^2-s^2], [x,y]);
[];  /* no solution found, but at least it returns */

/*
  Here is a workaround
  load(grobner);
  [x^2+y^2-d^2, (x-h)^2+(y-k)^2-s^2];
  poly_reduced_grobner(%, [x,y]);
  algsys(%, [x,y]);
 */
/* FIXME:  Testsuite issue.  Can't match form of solution 
algsys([2*k*y+2*h*x+s^2-k^2-h^2-d^2,
        (4*k^2+4*h^2)*y^2+(k*(4*s^2-4*d^2)-4*k^3-4*h^2*k)*y+s^4-2*d^2*s^2
          +h^2*((-2*s^2)+2*k^2-2*d^2)+k^2*(2*d^2-2*s^2)+k^4+h^4+d^4],[x,y]);
[[x = -(((k*sqrt((-s^4)+(2*k^2+2*h^2+2*d^2)*s^2-k^4+(2*d^2-2*h^2)*k^2-h^4
                           +2*d^2*h^2-d^4)
          +h*s^2-(h*k^2)-h^3-(d^2*h))
          /(2*k^2+2*h^2))),
  y = (h*sqrt((-s^2)+2*d*s+k^2+h^2-d^2)*sqrt(s^2+2*d*s-k^2-h^2+d^2)
          -k*s^2+k^3+(h^2+d^2)*k)
          /(2*k^2+2*h^2)],
 [x = (k*sqrt((-s^4)+(2*k^2+2*h^2+2*d^2)*s^2-k^4+(2*d^2-2*h^2)*k^2-h^4
                          +2*d^2*h^2-d^4)
          -h*s^2+h*k^2+h^3+d^2*h)
          /(2*k^2+2*h^2),
  y = -(((h*sqrt((-s^2)+2*d*s+k^2+h^2-d^2)*sqrt(s^2+2*d*s-k^2-h^2+d^2)
          +k*s^2-k^3+((-h^2)-d^2)*k)
          /(2*k^2+2*h^2)))]];
 */
 
/* SF bug #3210 ALGSYS does not return (regression)
 *   also #3212 ALGSYS throws error
 *
 * Note: %nrum is the counter for the %r variables introduced
 *       by algsys.  Set it to 0 for reproducable results
 */
block([%rnum:0], algsys([
 4*y*(y^2-28),
 8*y*(y^2-28)*z,
 -16*y*(y^2-23)*z,
 8*y*(y^2-10)*z,
 32*y*(z-y),
 32*y*(z+y),
 4*y*(y^2-28),
 8*y*(y^2-28)*z,
 -16*y*(y^2-23)*z,
 8*y*(y^2-10)*z,
 y*((4*y^2-112)*z^2-8*y^4+(185-4*x^2)*y^2),
 -2*y*((4*y^2-92)*z^2-2*y^4+(21-4*x^2)*y^2),
 y*((4*y^2-40)*z^2+(1-4*x^2)*y^2)],
[x,y,z]));
[[x=%r1,y=0,z=%r2]];

block([%rnum:0], algsys([
  -8*a*z,
  -3*(y-1)*y*z,-3*z*(z+y-1)*(z+y),
  2*(y-1)*y*z*(6*y*z-3*z-4*b*y^2-2*y^2+4*b*y+2*y),
  -8*a*z,
  -3*(y-1)*y*z,
  -4*z*((w^2-2*v*w+v^2+2*b-2*a)*z+8*a*y-4*a),
  -2*(y-1)*y*z*((6*y-3)*z+((-4*b)-2)*y^2+(4*b+2)*y),
  4*z
   *((2*w^2-4*v*w+2*v^2+2*b-1)*z^2
    +(((-2*w^2)+4*v*w-2*v^2-8*b+8*a-2)*y+(2*w+2*v-2)*x-4*v*w+4*b-4*a+2)
     *z-12*a*y^2+12*a*y-2*a),
  -z*(((4*w^2-8*v*w+4*v^2+14)*y^2+(((-8*w)-8*v+8)*x+16*v*w-18)*y+4*x^2
                                 -8*x+3)
     *z^2
     +(((-32*b)-16)*y^3+(48*b+24)*y^2+((-16*b)-8)*y)*z+(8*b-8*a+4)*y^4
     +((-16*b)+16*a-8)*y^3+(8*b-8*a+4)*y^2),
  -z*((4*w^2-8*v*w+4*v^2-1)*z^3+(((-16*w^2)+32*v*w-16*v^2-32*b)*y
                                +(16*w+16*v-16)*x-32*v*w+16*b+8)
                                *z^2
                               +((4*w^2-8*v*w+4*v^2+48*b-48*a+20)*y^2
                                +(((-8*w)-8*v+8)*x+16*v*w-48*b+48*a-24)
                                 *y+4*x^2-8*x+8*b-8*a+4)
                                *z+32*a*y^3-48*a*y^2+16*a*y),
  -2*z
    *(((4*w^2-8*v*w+4*v^2+2)*y+((-4*w)-4*v+4)*x+8*v*w-3)*z^3
     +(((-4*w^2)+8*v*w-4*v^2-24*b-8)*y^2
      +((8*w+8*v-8)*x-16*v*w+24*b+12)*y-4*x^2+8*x-4*b-2)
      *z^2+((16*b-16*a+8)*y^3+((-24*b)+24*a-12)*y^2+(8*b-8*a+4)*y)*z
     +4*a*y^4-8*a*y^3+4*a*y^2)],
[v,w,x,y,z]));
[[v=%r1,w=%r2,x=%r3,y=%r4,z=0]];

/* Solved by odelin for Kamke ODE 2.265.  New solution 2016-09 */
 algsys(
 [y = -1,
  z = 1,
  (-w^2)+2*v*w-v^2+5,2*((w+v-1)*x+4*w^2-10*v*w+4*v^2-22),
  (-x^2)-(12*w+12*v-12)*x+((-2*w)-2*v+2)*x+2*x-26*w^2+80*v*w-26*v^2+161,
  (-4*x^2)+((-8*w)-8*v+8)*x+8*x-4*w^2+24*v*w-4*v^2+48,
  2*(3*x^2+(13*w+13*v-13)*x+(6*w+6*v-6)*x-6*x+22*w^2-82*v*w+22*v^2-157),
  -2*((-6*x^2)-(12*w+12*v-12)*x+((-4*w)-4*v+4)*x+12*x-10*w^2+52*v*w
     -10*v^2+100),
  (-13*x^2)-(24*w+24*v-24)*x+((-26*w)-26*v+26)*x+26*x-41*w^2+182*v*w
     -41*v^2+344],
 [v,w,x,y,z]);
[[v =   (sqrt(5)+5)/2,  w = -((sqrt(5)-5)/2), x = 3,  y = -1, z = 1],
 [v = -((sqrt(5)-5)/2), w =   (sqrt(5)+5)/2,  x = 3,  y = -1, z = 1],
 [v =   (sqrt(5)-3)/2,  w = -((sqrt(5)+3)/2), x = -1, y = -1, z = 1],
 [v = -((sqrt(5)+3)/2), w =   (sqrt(5)-3)/2,  x = -1, y = -1, z = 1],
 [v =   (sqrt(5)+1)/2,  w = -((sqrt(5)-1)/2), x = 3,  y = -1, z = 1],
 [v = -((sqrt(5)-1)/2), w =   (sqrt(5)+1)/2,  x = 3,  y = -1, z = 1],
 [v =   (sqrt(5)+1)/2,  w = -((sqrt(5)-1)/2), x = -1, y = -1, z = 1],
 [v = -((sqrt(5)-1)/2), w =   (sqrt(5)+1)/2,  x = -1, y = -1, z = 1]];

/* Solved by odelin for Murphy ODE 1.182.  New solution 2016-10 */
/* FIXME:  Testsuite issue.  Can't match form of solution 
algsys(
 [x = -2*sqrt(-a1*a3),
  4*sqrt(-a1*a3)*((-4*a1*a3*(2*y-z))-4*a0*a3*sqrt(-a1*a3)),
  8*(-a1*a3)^(3/2)*(z^2-2*z-a2^2+2*a2)],
  [x,y,z]);
[[x = -2*sqrt(-a1*a3),
  y = -((a0*sqrt(-a1*a3)+a1*a2-2*a1)/(2*a1)),
  z = 2-a2],
 [x = -2*sqrt(-a1*a3),
  y = -((a0*sqrt(-a1*a3)-a1*a2)/(2*a1)),
  z = a2]];
 */

/* Solved by odelin for Kamke ODE 1.105.  New solution 2016-10 */
/* FIXME:  Testsuite issue.  Can't match form of solution
algsys(
 [4*sqrt(-a*c)*((-4*a*c*(2*y-z))-4*a*sqrt(-a*c)*d),
  8*(-a*c)^(3/2)*(z^2-2*z-b^2+2*b)],
 [y,z]);
[[y = -((sqrt(-a*c)*d+(b-2)*c)/(2*c)), z = 2-b],
 [y = -((sqrt(-a*c)*d-b*c)/(2*c)),     z = b]];
 */
 
/* Solved by odelin for Kamke ODE 1.291.  New solution 2016-10 */
algsys([
 -2*(18*y^2-36*x*y+18*x^2-5),
 (72*y+72*x-72)*z+72*y^2-288*x*y+72*x^2+69,
 (-36*z^2)-(144*y+144*x-144)*z+72*z-36*y^2+360*x*y-36*x^2-203,
 72*z^2-((-72*y)-72*x+72)*z-144*z-144*x*y+159,(-36*z^2)+72*z-35],
 [x,y,z]);
[[x =   (sqrt(10)-3)/12,   y = -((sqrt(10)+3)/12),  z = 5/6],
 [x = -((sqrt(10)+3)/12),  y =   (sqrt(10)-3)/12,   z = 5/6],
 [x =   (sqrt(10)+13)/12,  y = -((sqrt(10)-13)/12), z = 5/6],
 [x = -((sqrt(10)-13)/12), y =   (sqrt(10)+13)/12,  z = 5/6],
 [x =   (sqrt(10)+15)/12,  y = -((sqrt(10)-15)/12), z = 7/6],
 [x = -((sqrt(10)-15)/12), y =   (sqrt(10)+15)/12,  z = 7/6],
 [x =   (sqrt(10)-1)/12,   y = -((sqrt(10)+1)/12),  z = 7/6],
 [x = -((sqrt(10)+1)/12),  y =   (sqrt(10)-1)/12,   z = 7/6]];

/* SF bug #2038
   solve(eqs,[x,y]) gives correct solution
   solve(subst(sqrt(3),q,eqs),[x,y]) yields []!
*/
algsys(eqs:[y^2+x^2-(400-400/(2/q+1))^2=0,
       (-y-400/(2/q+1)+400)^2+x^2-640000/(2/q+1)^2=0],[x,y]);
[[x = -(400*q*sqrt(-((q-2)/(q+2)))), y = -((400*q^2-800)/(q+2))],
 [x =   400*q*sqrt(-((q-2)/(q+2))),  y = -((400*q^2-800)/(q+2))]];

algsys(subst(q=sqrt(3),eqs),[x,y]);
[[x = 1200-800*sqrt(3),y = 400*sqrt(3)-800],
 [x = 800*sqrt(3)-1200,y = 400*sqrt(3)-800]];

algsys(subst(q=1-sqrt(3),eqs),[x,y]);
[[x = -((25*2^(7/2)*sqrt(3-sqrt(3))*sqrt(sqrt(3)-1)*sqrt(3)
         +75*2^(7/2)*sqrt(3-sqrt(3))*sqrt(sqrt(3)-1))/3),
 y = 800/sqrt(3)],
[x = (25*2^(7/2)*sqrt(3-sqrt(3))*sqrt(sqrt(3)-1)*sqrt(3)
         +75*2^(7/2)*sqrt(3-sqrt(3))*sqrt(sqrt(3)-1))/3,
 y = 800/sqrt(3)]];

/* SF bug #2736 (in comments)
   Solving with p=1/(1-cos(1)) gave incorrect solution */
eqs:[x*y=p,x-y=3]$
[x*y = p,x-y = 3];

algsys(subst(p=1/(1-cos(1)),eqs),[x,y])$
[[x =   ((-sqrt(9*cos(1)^2-22*cos(1)+13))+3*cos(1)-3)/(2*cos(1)-2),
  y =   -((sqrt(9*cos(1)^2-22*cos(1)+13) +3*cos(1)-3)/(2*cos(1)-2))],
 [x =     (sqrt(9*cos(1)^2-22*cos(1)+13)+ 3*cos(1)-3)/(2*cos(1)-2),
  y = -(((-sqrt(9*cos(1)^2-22*cos(1)+13))+3*cos(1)-3)/(2*cos(1)-2))]];

float(%)$
[[x =  3.603649840080336, y =  0.603649840080336],
 [x = -0.603649840080336, y = -3.603649840080336]];

float(algsys(subst(p=float(1/(1-cos(1))),eqs),[x,y]))$
[[x = -0.6036497963666444,y = -3.603649796366644],
 [x =  3.603649796366644, y =  0.6036497963666444]];

/* SF bug 2059 - variable order dependent
   No solution found for certain solution variable orders
 */
(eqs:[z-a=%i*b,z+a=c,a*z+3*(a-z)=13-12*%i,3*(z-a)+a*z=12*%i+13],
algsys(eqs,[a,b,c,z]));
[[a = (-2*%i)-3, b = 4, c = -6, z = 2*%i-3],
 [a = 3-2*%i,    b = 4, c =  6, z = 2*%i+3]];

/* Count the number of solutions for each permuation of variable */
map(length,
  map(lambda([u],algsys(eqs,u)), listify(permutations([a,b,c,z]))));
[2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2];

(kill(eqt,eqs,F,G,p),done)$
done;
